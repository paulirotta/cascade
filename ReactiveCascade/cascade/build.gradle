apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    publishNonDefault true  // Gradle work-around, comment#35 https://code.google.com/p/android/issues/detail?id=52962

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "0.3.6"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // This enables long timeouts required on slow environments, e.g. Travis
    // https://code.google.com/p/android/issues/detail?id=189764
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // 10 minutes
        installOptions "-d","-t"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'InvalidPackage' // For Okio, https://github.com/square/okio/issues/58
        abortOnError false   // For RetroLambda, remove when possible
    }

    packagingOptions {
        exclude "LICENSE.txt" // For com.android.support.test:runner:0.2
    }

    dexOptions {
        incremental false
    }
//    preBuild.dependsOn('clean') // Allow normal 'run' to force clean of just the modified modules to keep retrolambda happy
}

// Lint configuration
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:23.2.1"
    compile "com.android.support:support-annotations:23.2.1"
    compile 'com.squareup.okhttp:okhttp:2.6.0'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'org.assertj:assertj-core:2.2.0'
}

// JavaDoc configuration
android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        //noinspection GroovyAssignabilityCheck
        classpath = files(variant.javaCompile.classpath.files) + files(android.bootClasspath)
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://developer.android.com/reference");
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}
